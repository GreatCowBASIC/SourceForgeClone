#chip 16F877A, 20
#mem 368

'LCD connection settings
#define LCD_IO 8
#define LCD_DATA_PORT PORTC
#define LCD_RS PORTD.0 
#define LCD_RW PORTD.1
#define LCD_Enable PORTD.2

'LED pins
#define Red PORTB.2
#define Green PORTB.1
#define Blue PORTB.0

'LED port directions
dir Red out
dir green out
dir blue out

'Intro message
'      1234567890123456
PRINT "Tri-colour LED:"
locate 1,1
PRINT "A GCBASIC Demo"
Wait 2 sec

Main:
 For RedLevel = 1 to 200
  ShowColour(Smooth(RedLevel), 1, 1)
 next
 For GreenLevel = 1 to 200
  ShowColour(1, Smooth(GreenLevel), 1)
 next
 For BlueLevel = 1 to 200
  ShowColour(1, 1, Smooth(BlueLevel))
 next
goto Main

sub ShowColour(RL, GL, BL)
 RLTemp = RL+RL
 FlashRate = ReadAD(AN0)
 for PulseLoop = 1 to FlashRate
  set red on
  set green on
  set blue on
  for Level = 1 to 200
   if Level > RLTemp then set red off
   if Level > GL then set green off
   if Level > BL then set blue off
  next
 next
end sub

function Smooth(temp)
 if temp < 100 then Smooth = temp
 if temp >= 100 then Smooth = 200-temp
end function