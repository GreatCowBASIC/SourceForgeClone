'''This program is a simple GLCD demonstration of read a pixel state on the GLCD - see the function ReadPixel_SSD1306()
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program is a simple GLCD demonstration of the SSD1306 GLCD capabilities.
'''This program examines the GLCD memory, see https://cdn.sparkfun.com/assets/learn_tutorials/4/7/7/Font1_MicroView_5x8_FontMapping.png
'''
'''The SSD1306 controller a small graphical OLED, suitable for a lot of various projects.
'''The display is 38*35 mm, with an active display surface of 0.96inches , and a 128*64 pixel resolution.
'''The display is easy to interface, using an I2C type of communication.
'''The GLCD is connected to the microprocessor as shown in the hardware section of this code.
'''
'''The terminal is connect to the serial port of the microcontroller at 115200bps.
'''
'''@author  EvanV
'''@licence GPL
'''@version 1.4
'''@date    11.4.17
'''********************************************************************************

; ----- Configuration
  #chip mega328p,16
  #option explicit
  #include <glcd.h>

; ----- Define Hardware settings
  ' Define I2C settings
  #define HI2C_BAUD_RATE 400
  #define HI2C_DATA
  HI2CMode Master

; ----- Define GLCD Hardware settings
  #define GLCD_TYPE GLCD_TYPE_SSD1306
  #define GLCD_I2C_Address 0x78

  'USART settings
  #define USART_BAUD_RATE 115200  'Initializes USART port with specific baud
  #define USART_TX_BLOCKING ' wait for tx register to be empty
  #define USART_DELAY 0 ms

; ----- Define variables

  Dim PixelValue, xPos, yPos, ReadxPos, ReadyPos as Byte



; ----- Main program
     HSerPrintCRLF 2
     HSerPrint "A program to examine the state of a GLCD pixel"
     HSerPrintCRLF 2


     xPos = 0                                                       'xPos of our pixel to set
     yPos = 0                                                       'yPos of our pixel to set
     ReadxPos = 0                                                   'xPos of our pixel to examine
     ReadyPos = 0                                                   'yPos of our pixel to examine


     HSerPrint "State of X"
     HSerPrint ReadxPos
     HSerPrint ", Y"
     HSerPrint ReadyPos
     HSerPrint " equals "


     PixelValue = 0x55                                              'Set PixelValue to something that cannot be returned by the function
     wait 100 ms                                                    'Let things settle
     Pset ( xPos, yPos, 1 )                                         'Set a pixel

     'This is the read Function ReadPixel_SSD1306()
     PixelValue = ReadPixel_SSD1306( ReadxPos, ReadyPos )        'Read the pixel state

     HSerPrint PixelValue                                           'Print the result
     HSerPrintCRLF


     wait 100 ms                                                    'Let things settle
     End


  '''Examine a pixel on the GLCD
  '''@param GLCDX X coordinate of pixel
  '''@param GLCDY Y coordinate of pixel
  dim GLCDBitNo, GLCDChange, GLCDDataTemp as byte
  Function ReadPixel_SSD1306(In GLCDX, In GLCDY )
      dim GLCDBitNo, GLCDChange, GLCDDataTemp as byte
      'Examine pixel at X, Y on LCD to State
      'X is 0 to 127
      'Y is 0 to 63
      'Origin in top left

    #if GLCD_TYPE = GLCD_TYPE_SSD1306


            'Select correct buffer element
            SSD1306_BufferLocationCalc = GLCDY
            'Divide by 8
            Repeat 3
              Set C Off
              Rotate SSD1306_BufferLocationCalc Right
            End Repeat
            SSD1306_BufferLocationCalc = SSD1306_BufferLocationCalc * GLCD_WIDTH
            SSD1306_BufferLocationCalc = GLCDX + SSD1306_BufferLocationCalc+1  'add 1 as we dont use element (0)
            GLCDDataTemp = SSD1306_BufferAlias(SSD1306_BufferLocationCalc)

            'Change data to examine pixel by rotating our bit to bit zero
            GLCDBitNo = GLCDY And 7
            Repeat GLCDBitNo
              Rotate GLCDDataTemp right
            End Repeat


            ReadPixel_SSD1306 = GLCDDataTemp.0

    #endif
  end function
