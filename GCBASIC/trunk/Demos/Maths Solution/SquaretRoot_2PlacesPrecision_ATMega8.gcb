'''A program  for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program shows the first 100 square roots to 2 decimal places.
'''This uses a binary approach involving only bit-shifting,
'''addition and subtraction for greatest speed. The inputs
'''and output are Words, but the internal computation is
'''carried out on Longs for best accuracy. The returned
'''value is accurate to 2 decimal places. The maximum
'''argument is 4294.

'''input parameter: word
'''output value: word
'''local variables: sqrt_result, sqrt_bitFlag, sqrt_num: long

'''@author 		EvanV based on works of ThomasH
'''@licence		GPL
'''@version		1.0a
'''@date   		01.04.2015
'''********************************************************************************

; ----- Configuration

 #chip mega8,8
 #option explicit

  #include <maths.h>

 'Use LCD in 2-wire mode with 74LS164 / 74HC164
  #define LCD_IO 2
  #define LCD_DB PORTC.0            ; databit
  #define LCD_CB PORTC.1            ; clockbit
  #define LCD_NO_RW


; ----- Variables
 dim ii as word
 dim outStr, valStr as string
 Dim LENGTH as Byte


; ----- Main body of program commences here.

    for ii = 0 to 100                ;print first 100 square roots
    cls
    print "sqrt("
    print ii
    print ")="

    valStr = str(sqrt(ii))         ;format decimal nicely
    length = len(valStr)

    select case length
     case 1:
        outStr = "0.00"           ;zero case
     case 3:
        outStr = left(valStr,1)+ "."+right(valStr,2)
      case 4:
        outStr = left(valStr,2)+ "."+right(valStr,2)
      case 5:
        outStr = left(valStr,3)+ "."+right(valStr,2)
    end select

    print outStr                  ;display results
    wait 2 S
  next ii

; ----- Support methods.  Subroutines and Functions
