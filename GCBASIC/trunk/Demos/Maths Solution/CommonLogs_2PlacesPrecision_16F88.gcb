'''A program  for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program shows common logarithms to two decimal places.
'''
'''
'''@author 		EvanV based on works of ThomasH
'''@licence		GPL
'''@version		1.0a
'''@date   		01.04.2015
'''********************************************************************************

; ----- Configuration

 #chip 16F88, 8                  
 #config mclr=off                ;reset handled internally
 #config osc=int                 ;use internal clock

 #include <maths.h>


; ----- Constants
 #define LCD_IO      4           ;4-bit mode
 #define LCD_RS      PortB.2
 #define LCD_Enable  PortB.3
 #define LCD_DB4     PortB.4
 #define LCD_DB5     PortB.5
 #define LCD_DB6     PortB.6
 #define LCD_DB7     PortB.7
 #define LCD_NO_RW

; ----- Define Hardware settings
 dir PortB out                   ;all outputs to the LCD 

; ----- Variables
 dim ii as word
 dim outStr, valStr as string


; ----- Main body of program commences here.

  for ii = 1 to 65535      
    cls
    print "log10("
    print ii                       ;print argument
    print ")="
    locate 1,0
    print2Places(log10(ii))        ;and its logarithm
    wait 2 S
  next   

; ----- Support methods.  Subroutines and Functions

  sub print2places(in p2p_arg as word)
    ;print decently formatted results, to 2 decimal places

    valStr = str(p2p_arg)
    select case len(valStr)
      case 1:
        outStr = "0.0" + valStr
      case 2:
        outStr = "0." + valStr
      case 3:
        outStr = left(valStr,1) + "." + right(valStr,2)
      case 4:
        outStr = left(valStr,2) + "." + right(valStr,2)      
    end select    
    print outStr
  end sub
