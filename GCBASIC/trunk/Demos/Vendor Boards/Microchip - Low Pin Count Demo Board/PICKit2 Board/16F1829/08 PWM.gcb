'''
'''******************************************************************
''' Lesson 8 - "PWM"
'''
''' Pulse-Width Modulation (PWM) is a scheme that provides power to a load by switching
''' quickly between fully on and fully off states. The PWM signal resembles a square wave
''' where the high portion of the signal is considered the on state and the low portion of
''' the signal is considered the off state. The high portion, also known as the pulse width,
''' can vary in time and is defined in steps. A longer, high on time will illuminate the LED
''' brighter. The frequency or period of the PWM does not change. A larger number of
''' steps applied, which lengthens the pulse width, also supplies more power to the load.
''' Lowering the number of steps applied, which shortens the pulse width, supplies less
''' power. The PWM period is defined as the duration of one complete cycle or the total
''' amount of on and off time combined.
'''
''' Demo1.  Rotating the POT will adjust the brightness of a single LED, DS4
''' Demo2.  Shows a how the changing PWM values across the LEDs
'''
'''  PIC: 16F1829
'''  Compiler: GCB
'''  IDE: GCB@SYN
'''
'''  Board: PICkit 2 Low Pin Count Demo Board
'''  Date: 10.4.2015
'''
''''''******************************************************************
''' See Low Pin Count Demo Board User's Guide for Lesson Information*
''''''*****************************************************************
'''


 #chip 16f1829,8
 #config INTOSC , WDTE_OFF , PWRTE_OFF , MCLRE_OFF , CP_ON , CPD_OFF , BOREN_ON , CLKOUTEN_OFF , IESO_OFF

 #config WRT_OFF , PLLEN_OFF , STVREN_OFF , LVP_OFF


 #define demo1
' #define demo2

    '' -------------------LATC-----------------
    '' Bit#:  -7---6---5---4---3---2---1---0---
    '' LED:   ---------------|DS4|DS3|DS2|DS1|-
    ''-----------------------------------------
    ''

 #script
        'This script provides PWM results!

        'reference voltage                        5v
        'actual voltage at port when 100% Pot     4.15
        'adc max                                  1023
        'number of graduations requried           255
        reference_voltage = 4.15
        vcc = 5
        numberofgrads = 255
        maxADC = 1023
        ADC_adjustment = int((reference_voltage*100)/(vcc*10))
        ADC_adjustment = int(ADC_adjustment * maxADC / numberofgrads )

 #endscript

  #ifdef demo1
      dir portc out
      dir portc.3 out
      dim calculatedPWM as word

      do
          wait 5 ms
          calculatedPWM = ( ( ReadAD10 ( AN0 ) * 10  ) / ADC_adjustment   )
          if calculatedPWM > 255 then calculatedPWM = 255
          HPWM 2, 40, calculatedPWM
      loop

  #endif


  #ifdef demo2
    dir portc.5 out    ; CCP1
    dir portc.3 out    ; CCP2
    dir porta.2 out    ; CCP3
    dir portc.6 out    ; CCP4

    do forever
            For Bright = 1 to 255
                HPWM 3, 40, Bright
                HPWM 2, 40, Bright
                HPWM 1, 40, Bright
                HPWM 4, 40, Bright
                wait 10 ms
            next

    loop
  #endif